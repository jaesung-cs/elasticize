#version 450

#extension GL_KHR_shader_subgroup_basic : enable
#extension GL_KHR_shader_subgroup_arithmetic : enable
#extension GL_KHR_shader_subgroup_ballot : enable
#extension GL_KHR_shader_subgroup_vote : enable

const int BLOCK_SIZE = 256;
const int RADIX_BITS = 8;
const int RADIX_SIZE = 1 << 8;

layout (local_size_x = BLOCK_SIZE) in;

layout (push_constant) uniform SortInfoUbo {
  uint array_size;
  int bit_offset;
  uint scan_offset;
};

layout (std430, binding = 1) buffer CounterSsbo {
  uint data[]; // [key][groupIndex]
} counter;

shared uint local_prefix_sum[RADIX_SIZE];
shared uint subgroup_counter[gl_WorkGroupSize.x];
shared uint subgroup_prefix_sum[gl_WorkGroupSize.x];

void main() {
  uint item;
  if (gl_GlobalInvocationID.x < array_size)
    item = counter.data[scan_offset + gl_GlobalInvocationID.x];
  else
    item = 0;

  // Prefix sum for each subgroup
  local_prefix_sum[gl_LocalInvocationID.x] = subgroupExclusiveAdd(item);
  subgroupBarrier();

  // One exceution in subgroup
  if (gl_LocalInvocationID.x % gl_SubgroupSize == gl_SubgroupSize - 1)
    subgroup_counter[gl_LocalInvocationID.x / gl_SubgroupSize] = local_prefix_sum[gl_LocalInvocationID.x] + item;
  barrier();

  // The first subgroup deal with subgroup prefix sum
  // Assuming gl_NumSubgroups < gl_SubgroupSize
  if (gl_LocalInvocationID.x < gl_NumSubgroups)
    subgroup_prefix_sum[gl_LocalInvocationID.x] = subgroupExclusiveAdd(subgroup_counter[gl_LocalInvocationID.x]);
  barrier();

  // Spread to subgroups
  local_prefix_sum[gl_LocalInvocationID.x] += subgroup_prefix_sum[gl_LocalInvocationID.x / gl_SubgroupSize];
  barrier();

  // Update counter prefix sum
  if (gl_GlobalInvocationID.x < array_size)
    counter.data[scan_offset + gl_GlobalInvocationID.x] = local_prefix_sum[gl_LocalInvocationID.x];

  // Update to the next level counter
  if (gl_LocalInvocationID.x == gl_WorkGroupSize.x - 1)
    counter.data[scan_offset + array_size + gl_WorkGroupID.x] = local_prefix_sum[gl_WorkGroupSize.x - 1] + item;
}
